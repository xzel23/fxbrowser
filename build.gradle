plugins {
  id "com.dua3.gradle.jpms" version "0.4.1"
  id "com.github.spotbugs" version "1.6.3"
}

apply plugin: 'java'

// add access rules for javafx and javascript, setup eclipse module path
import org.gradle.plugins.ide.eclipse.model.AccessRule
apply plugin: 'eclipse'

eclipse {
    classpath {
        file {
            whenMerged {
                def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }
                jre.accessRules.add(new AccessRule('0', 'javafx/**'))
                jre.accessRules.add(new AccessRule('0', 'netscape/javascript/**'))
                
                entries.findAll { it.kind=='lib' }.each { it.entryAttributes['module'] = 'true' }
            }
        }
    }
}

sourceCompatibility = 11
targetCompatibility = 11

compileJava {
    options.encoding = 'UTF-8'
}

repositories {
    jcenter()
    maven { url  "https://dl.bintray.com/dua3/public" }
}

// get OS (because JavaFX packages are OS dependent)
def currentOS = org.gradle.internal.os.OperatingSystem.current()
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

dependencies {
    // utility - collection  of utility classes (my own code)  [ASL 2.0]
    compile group: 'com.dua3.utility', name: 'utility', version: '0.15.0'

    // JavaFX
    compile "org.openjfx:javafx-base:11:${platform}"
    compile "org.openjfx:javafx-fxml:11:${platform}"
    compile "org.openjfx:javafx-graphics:11:${platform}"
    compile "org.openjfx:javafx-controls:11:${platform}"
    compile "org.openjfx:javafx-web:11:${platform}"
    compile "org.openjfx:javafx-media:11:${platform}"

    // FontAwesomeFX - icon collection [FontAweSomeFX: ASL 2.0, FontAweSome: SIL OFL 1.1 / Creative Commons]
    compile 'de.jensd:fontawesomefx-fontawesome:4.7.0-9.1.2-jpms'
        
    // JUnit - unit testing [EPL]
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

jlink {
	module = 'com.dua3.fxbrowser'
	main = 'com.dua3.fxbrowser.FxBrowser'
	application = 'fxbrowser'
	compress = 2
    debug = false
}

// SpotBugs version 3.1.7 is needed for JDK 11 compatibility
spotbugs.toolVersion = '3.1.7'
