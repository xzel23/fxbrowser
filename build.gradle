plugins {
  id 'java'
  id 'application'
  id 'eclipse'
  id "com.dua3.gradle.jpms" version "0.6"
  id "com.github.spotbugs" version "1.6.5"
}

sourceCompatibility = 8
targetCompatibility = 8

mainClassName = 'com.dua3.fxbrowser.Main'

compileJava {
    options.encoding = 'UTF-8'
}

repositories {
    jcenter()
    maven { url  "https://dl.bintray.com/dua3/public" }
}

// get OS (because JavaFX packages are OS dependent)
def currentOS = org.gradle.internal.os.OperatingSystem.current()
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

dependencies {
    // utility - collection  of utility classes (my own code)  [ASL 2.0]
    def utilityVersion = "1.2.4"
    compile "com.dua3.utility:utility:${utilityVersion}"
    
    // JavaFX
    compile "org.openjfx:javafx-base:11:${platform}"
    compile "org.openjfx:javafx-fxml:11:${platform}"
    compile "org.openjfx:javafx-graphics:11:${platform}"
    compile "org.openjfx:javafx-controls:11:${platform}"
    compile "org.openjfx:javafx-web:11:${platform}"
    compile "org.openjfx:javafx-media:11:${platform}"

    // FontAwesomeFX - icon collection [FontAweSomeFX: ASL 2.0, FontAweSome: SIL OFL 1.1 / Creative Commons]
    compile 'de.jensd:fontawesomefx-fontawesome:4.7.0-9.1.2-jpms'
}

jlink {
	module = 'com.dua3.fxbrowser'
	main = 'com.dua3.fxbrowser.FxBrowser'
	application = 'fxbrowser'
	compress = 2
    debug = false
}
